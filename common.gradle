// Gradle settings and tasks common to all subprojects

apply plugin: 'java'

group = 'com.github.stephengold'
ext { // current versions of various libraries
    jbulletVersion = '1.0.1'
    vecmathVersion = '1.5.2'
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // Java compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        //options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
}

repositories {
    //mavenLocal()
    mavenCentral()
    flatDir { dirs rootProject.file('lib/lwjgl') }
    flatDir { dirs rootProject.file('lib/swing-layout') }
}

// custom tasks for creating source/javadoc JARs
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    description 'Create a JAR of source files.'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.allJava
}
task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Create a JAR of javadoc.'
    from javadoc.destinationDir
}
