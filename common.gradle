// Gradle settings and tasks common to all jbullet subprojects

apply plugin: 'java'

group = 'com.github.stephengold'
ext {
    // module coordinates of dependencies:
    stackallocCoordinates = 'com.github.stephengold:stack-alloc:1.0.2'
    vecmathCoordinates = 'javax.vecmath:vecmath:1.5.2'

    // current versions of libraries:
    jbulletVersion = '1.0.3-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_14)) {
        // Suppress warnings that source value 7 is obsolete.
        options.compilerArgs << '-Xlint:-options'
    }
    //options.compilerArgs << '-Xlint:unchecked' // to provide detailed warnings
    options.deprecation = true
    options.encoding = 'UTF-8'
}

repositories {
    //mavenLocal()
    mavenCentral()
    flatDir { dirs rootProject.file('lib/lwjgl') }
    flatDir { dirs rootProject.file('lib/swing-layout') }
}
